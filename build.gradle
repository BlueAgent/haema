plugins {
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm"
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

minecraft {
}

repositories {
    maven {
        name = "FabricMC"
        url = "https://maven.fabricmc.net/"
    }
    maven {
        name = "JitPack"
        url 'https://jitpack.io'
    }
    maven {
        name = "Sonatype"
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        name = "Jamies"
        url "https://maven.jamieswhiteshirt.com/libs-release/"
        content {
            includeGroup "com.jamieswhiteshirt"
        }
    }
    maven {
        name = "Jared"
        url 'https://maven.blamejared.com'
        content {
            includeGroup "vazkii.patchouli"
        }
    }
    maven {
        name = "Ladysnake Libs"
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
        content {
            includeGroup "io.github.ladysnake"
            includeGroupByRegex "io\\.github\\.onyxstudios.*"
        }
    }
    maven {
        name = "GeckoLib"
        url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven"
        content {
            includeGroup "software.bernie.geckolib"
        }
    }
    maven {
        name = "Cotton"
        url = "https://server.bbkr.space/artifactory/libs-release/"
        content {
            includeGroup "io.github.cottonmc"
        }
    }
    maven {
        name = "Shedaniel"
        url = "https://maven.shedaniel.me"
        content {
            includeGroupByRegex "dev\\.architectury.*"
            includeGroupByRegex "me\\.shedaniel.*"
            includeGroupByRegex "me\\.sargunvohra.*"
        }
    }
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/releases/"
        content {
            includeGroup "io.github.prospector"
            includeGroup "com.terraformersmc"
        }
    }
    maven {
        name = "Architectury"
        url "https://maven.architectury.dev/"
        content {
            includeGroup "dev.architectury"
        }
    }
    maven {
        name = "cafeteria.dev"
        url = 'https://maven.cafeteria.dev'
        content {
            includeGroup 'net.adriantodt.fabricmc'
        }
    }
    maven {
        name = "Flemmli97"
        url "https://gitlab.com/api/v4/projects/21830712/packages/maven"
        content {
            includeGroup 'io.github.flemmli97'
        }
    }
    maven {
        name = "Gunpowder"
        url = "https://maven.martmists.com/releases"
        content {
            includeGroup 'io.github.gunpowder'
        }
    }
    maven {
        name = "Permissions"
        url = "https://maven.nucleoid.xyz"
        content {
            includeGroup 'eu.pb4'
        }
    }
    maven {
        name = "HavenKing"
        url = "https://hephaestus.dev/release"
        content {
            includeGroup 'dev.inkwell'
        }
    }
    maven {
        name = "CurseMaven"
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    mavenLocal()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    ///
    /// HARD DEPS
    ///

    // Fabric API
    modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}") {
        exclude module: "fabric-dimensions-v1"
        exclude module: "fabric-biomes-v1"
    }

    // Fabric Language Kotlin
    modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

    ///
    /// INCLUDED
    ///

    // Satin
    modImplementation "io.github.ladysnake:satin:${project.satin_version}"
    include "io.github.ladysnake:satin:${project.satin_version}"

    // Reach Entity Attributes
    modImplementation "com.jamieswhiteshirt:reach-entity-attributes:${project.rea_version}"
    include "com.jamieswhiteshirt:reach-entity-attributes:${project.rea_version}"

    // Patchouli
    modImplementation "vazkii.patchouli:Patchouli:${project.patchouli_version}"
    include "vazkii.patchouli:Patchouli:${project.patchouli_version}"

    // Cloth Config
    modApi include("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}")

    // CCA
    modApi "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cc_version}"
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cc_version}"
    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${project.cc_version}"
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${project.cc_version}"

    // LibCD
    //include "io.github.cottonmc:LibCD:${project.libcd_version}"
    //modRuntime "io.github.cottonmc:LibCD:${project.libcd_version}"

    // Fabric Permissions API
    modImplementation "me.lucko:fabric-permissions-api:${project.fpa_version}"
    include "me.lucko:fabric-permissions-api:${project.fpa_version}"

    ///
    /// SOFT DEPS
    ///

    // REI
    modImplementation "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"

    // Origins
    modImplementation("com.github.apace100:origins-fabric:${project.origins_version}") {
        exclude module: "modmenu"
        exclude group: "io.github.OnyxStudios.Cardinal-Components-API"
    }

    // Rat's Mischief
    modCompileOnly "curse.maven:rats-mischief-431787:${project.rats_version}"
    modCompileOnly("software.bernie.geckolib:geckolib-fabric-1.18:${project.geckolib_version}") {
        exclude group: "com.eliotlash.mclib"
        exclude group: "com.eliotlash.molang"
    }

    // Bewitchment
    modCompileOnly "curse.maven:bewitchment-394915:${project.bewitchment_version}"

    // Flan
    modCompileOnly "io.github.flemmli97:flan:${project.flan_version}"

    // ModMenu
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

java {
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        // Make filename unique
        rename { "${it}_${project.archivesBaseName}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        // Add repositories to publish to here.
    }
}

compileKotlin.kotlinOptions.jvmTarget = "16"

/**
 * https://dev.to/williambl/mixins-kotlin-and-default-methods-why-they-don-t-work-together-417d
 */
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += '-Xjvm-default=all'
    }
}
